/*
 *
 * Copyright (c) 2016. Enterprise Architecture Group, EACG
 *
 * SPDX-License-Identifier:	MIT
 *
 */


group 'de.eacg'
version '0.1.3'

buildscript {
    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
//        classpath "de.eacg:ecs-gradle-plugin:0.1.3"               // uncomment to scan this compoment
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.gradle.plugin-publish'
// apply plugin: 'de.eacg.ecsPlugin'                               // uncomment to scan this compoment


sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenLocal()
    mavenCentral()
}

processResources {
    expand(project.properties)
}

nexusStaging {
    // packageGroup = "org.mycompany.myproject" //optional if packageGroup == project.getGroup()
    stagingProfileId = "3214c3fab3cb2b" //when not defined will be got from server using "packageGroup"
}

/*
ecsPlugin {                                                       // uncomment to scan this compoment
    credentials = '~/.ecsrc.json'
    verbose = true
}
*/

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'de.eacg:ecs-java-client:0.1.0'
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

archivesBaseName = 'ecs-gradle-plugin'
if (!hasProperty('ossrhUsername')) {
    ext.ossrhUsername = ''
}
if (!hasProperty('ossrhPassword')) {
    ext.ossrhPassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ECS gradle plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'EACG Code Scan gradle plugin. A gradle plugin to transfer dependency information to ECS server.'

                url 'https://github.com/eacg-gmbh/ecs-gradle-plugin'

                scm {
                    url 'https://github.com/eacg-gmbh/ecs-gradle-plugin'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://raw.githubusercontent.com/eacg-gmbh/ecs-gradle-plugin/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gdd'
                        name 'Gerhard Dickescheid'
                        email 'gdd@eacg.de'
                        organization 'EACG, Enterprise Architecture Consulting Group GmbH'
                        organizationUrl 'https://www.eacg.de/'
                    }
                }
            }
        }
    }
}


pluginBundle {
    website = 'https://github.com/eacg-gmbh/ecs-gradle-plugin'
    vcsUrl = 'https://github.com/eacg-gmbh/ecs-gradle-plugin'
    description = 'EACG Code Scan gradle plugin. A gradle plugin to transfer dependency information to ECS server.'
    tags = ['ECS', 'dependency']

    plugins {
        ecsGradlePlugin {
            id = 'de.eacg.ecsPlugin'
            displayName = 'EACG Code Scan gradle plugin'
        }
    }
}
